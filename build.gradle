buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
        assertJVersion = "3.8.0"
        mockitoVersion = "1.10.19"
        groovyVersion = "2.4.13"
        spockVersion = "1.1-groovy-2.4"

    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

configure(allprojects) {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: "io.spring.dependency-management"

    group = 'io.iamkyu'
    version = '1.0.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile("org.projectlombok:lombok")
        runtime 'com.h2database:h2:1.4.190'

        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("org.assertj:assertj-core:${assertJVersion}")
        testCompile("org.mockito:mockito-all:${mockitoVersion}")
        testCompile("org.codehaus.groovy:groovy-all:${groovyVersion}")
        testCompile("org.spockframework:spock-core:${spockVersion}")


    }

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging' // replaced with jcl-over-slf4j
        all*.exclude group: 'log4j', module: 'log4j' // replaced with log4j-over-slf4j
    }

    task initSrc {
        doLast {
            project.sourceSets*.allSource.srcDirTrees.flatten().dir.each { dir ->
                dir.mkdirs()
            }
        }
    }
}

project(':oauth2-server') {
    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.springframework.boot:spring-boot-starter-jdbc'
        compile 'org.springframework.boot:spring-boot-starter-security'

        compile 'org.springframework.security.oauth:spring-security-oauth2'
        compile 'org.springframework.security:spring-security-jwt'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }
}

project(':api-server') {
    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-data-jpa'
        compile 'org.springframework.boot:spring-boot-starter-data-rest'
        compile 'org.springframework.boot:spring-boot-starter-security'

        compile 'org.springframework.security.oauth:spring-security-oauth2'
        compile 'org.springframework.data:spring-data-rest-hal-browser'
        compile 'org.springframework.security:spring-security-jwt'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }
}

project('my-auth-server') {
    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-jdbc") {
            exclude module: "org.apache.tomcat:tomcat-jdbc"
        }
        compile("org.springframework.boot:spring-boot-starter-actuator")
        runtime("mysql:mysql-connector-java")
        compile("com.zaxxer:HikariCP")
    }
}
